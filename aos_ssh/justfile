#!/usr/bin/env just --justfile
export PATH := join(justfile_directory(), ".env", "bin") + ":" + env_var('PATH')
set positional-arguments

# Default recipe to display help
default:
    @just --list

# version := `sh -lc 'git describe --tags --abbrev=0 2>/dev/null || echo 0.0.0'`
version_ := `cat VERSION`

alias install := sync
# Install dependencies with uv
sync:
    uv sync

# Install development dependencies
install-dev:
    uv sync --dev

alias run := dev
# Run the aos_ssh FastAPI service
dev:
    @echo "ðŸ  Starting aos_ssh..."
    uv run ale_aos_ssh

alias black := format
# Ruff can format the code
format: install-dev
    @echo "ðŸ“  Ruff is formating the code..."
    -uv run ruff format .

alias lint := check
# Ruff can check the code
check: install-dev
    @echo "ðŸ”§  Ruff is checking the code..."
    -uv run ruff check .

# Show current version
version:
    @echo "ðŸ·ï¸  Version from file: {{version_}}, latest git tag: `git describe --tags --abbrev=0 2>/dev/null`"

# ðŸ·ï¸ Tag Git
tag:
    @echo "ðŸ·ï¸ Tag Git"
    git tag -a v{{version_}} -m "Release {{version_}}" && git push origin v{{version_}}

# Bump version manually
bump new_version:
    @echo "â¬†ï¸  Updating version to {{new_version}}..."
    echo "{{new_version}}" > VERSION

# Run pre-commit checks
precommit: version format check
    @echo ""
    @echo "ðŸ·ï¸  Version : {{version_}} current tag: `git describe --tags --abbrev=0 2>/dev/null`"
    @echo ""
    @echo "ðŸ”„  Modified (unstaged):"
    @git diff --name-only
    @echo ""
    @echo "âœ…  Modified (staged):"
    @git diff --cached --name-only
    @echo ""
    @echo "ðŸ†•  Untracked files:"
    @git ls-files --others --exclude-standard
    @echo ""

# Build Python package into source distributions and wheels
dist:
    @echo "ðŸ”¨  Building the package..."
    uv build

# Upload distributions to pypi.org
publish:
    @echo "ðŸ“¤  Uploading distributions to pypi.org..."
    uv publish

# Build Docker image
build *OPT:
    @echo "ðŸ”¨ï¸  Building image verion {{version_}}..."
    docker build --build-arg VERSION={{version_}} -t ale-aos-ssh:{{version_}} -t ale-aos-ssh:latest {{OPT}} .

# Start Docker container
start:
    @echo "ðŸ§ª  Starting container..."
    docker run -it --name ale-aos-ssh --rm -p 8120:8110 \
      -v "{{justfile_directory()}}/data/aos-ssh-host-brest.json:/app/data/aos-ssh-host.json" \
      -v "{{justfile_directory()}}/data/aos-ssh-conf.yaml:/app/data/aos-ssh-conf.yaml" \
      ale-aos-ssh:latest

# Stop Docker container
stop:
    @echo "ðŸ›‘  Stopping container..."
    -docker stop ale-aos-ssh 2>/dev/null || true

# Restart container
restart: stop start

# Show critical and high severity vulnerabilities of image
scout:
    @echo "ðŸ“Š  Scanning image"
    docker scout cves --only-severity critical,high --details ale-aos-ssh:latest

# Scan container's image to find vulnerabilities (CVE)
trivy:
    trivy image ale-aos-ssh:latest

# Clean local Docker cache
clean:
    @echo "ðŸ§¹  Cleaning local Docker cache..."
    docker system prune -f